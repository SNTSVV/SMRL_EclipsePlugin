import static smrl.mr.language.Operations.*;
import smrl.mr.language.Action;

package smrl.mr.owasp {
 
/**
 * A login operation should not succeed if performed on the HTTP channel.
 *  
 * The 1st parameter of the operator IMPLIES is a boolean expression with three clauses 
 * joined with logical conjunctions. 
 * The 1st clause checks if the current action performs a log in. 
 * The 2nd clause defines the follow-up input. 
 * The 3rd clause changes the channel of the login action in the follow-up input. 
 * 
 * The 2nd parameter of IMPLIES checks if the current action is under an encrypted channel, 
 * or the output generated by the login operation is different in the two cases.
 */
MR OTG_AUTHN_001 { 
 { 
    for ( Action action : Input(1).actions() ) {
	  	var pos = action.getPosition();
	   IMPLIES(
	        isLogin(action) 												//1st par (1st clause)
	          &&  EQUAL ( Input(2), Input(1) )				    			//1st par (2nd clause)
	    	  &&  Input(2).actions.get(action.position).setChannel("http")  //1st par (3rd clause)
		    ,  
		    OR(
		    	NOT(isEncrypted(action)),									//2nd par of IMPLIES (1st clause)
		    	different ( Output(Input(1),pos),  Output(Input(2),pos) ) 	//2nd par of IMPLIES (2nd clause)
		    	)
		);//end-IMPLIES
	}//end-for
 } 
}//end-MR
}//end-package





