import static smrl.mr.language.Operations.*;
import smrl.mr.language.Action;

package smrl.mr.owasp{

/**
 * 
 * Duplicating a parameter value should not lead to a different behaviour in the system.
 * 
 * 
 * The first loop iterates over all the actions of an input sequence.
 * The second loop iterates over all the parameters.
 * 
 * The 1st parameter of IMPLIES is made of two clauses.
 * The 1st clause defines a follow-up input.
 * The 2nd clause duplicates one parameter.
 * 
 * 
 * The 2nd parameter of IMPLIES verifies that the output generated by the modified action is 
 * the same in the two cases.
 *   
 */ 
	MR OTG_INPVAL_004 {
		{
			for(Action action : Input(1).actions() ){
				for(var par=0; par < action.getParameters().size(); par++ ){ 
					var pos = action.getPosition();
					IMPLIES( 
						EQUAL(Input(2), Input(1) ) &&
						Input(2).actions().get(pos).addParameter(
						    action.getParameterName(par), 
						    action.getParameterValue(par) 
					    )
						,
						EQUAL(Output(Input(1), pos), Output( Input(2), pos))
					);//end-IMPLIES
				}//end-for
			}//end-for
		}
	}//end-MR
}//end-package

